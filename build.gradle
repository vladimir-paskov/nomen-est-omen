plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
}


group = 'com.vmsoft'
version = '2.1.1'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}
rootProject.description = 'Random names generator; for IDs or fun.'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "standardOut", "passed", "skipped", "failed"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

//
// MAVEN
//

ext.admin = hasProperty('github_user')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'nomen-est-omen'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Nomen est Omen'
                description = 'Random names generator; for IDs or fun.'
                url = 'https://www.vmsoft-bg.com'
                licenses {
                    license {
                        name = 'BSD-2-Clause'
                        url = 'https://github.com/vladimir-paskov/nomen-est-omen/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'vmsoft'
                        name = 'Vladimir Paskov, original work by Igor SpasiÄ‡'
                        email = 'support@vmsoft-bg.com'
                        timezone = '+2'
                    }
                }
                scm {
                    url = 'https://github.com/vladimir-paskov/nomen-est-omen'
                    connection = 'scm:git:git://github.com/vladimir-paskov/nomen-est-omen.git'
                    developerConnection = 'scm:git:ssh://git@github.com/vladimir-paskov/nomen-est-omen.git'
                }
            }
        }
    }
    repositories {
        maven {
            name "GitHubPackages"
            url "https://maven.pkg.github.com/vladimir-paskov/nomen-est-omen"
            if (admin) {
                credentials {
                    username "${github_user}"
                    password "${github_token}"
                }
            }
        }
    }
}

//signing {
    //required { admin }
    //sign publishing.publications.mavenJava
//}

task install(dependsOn: publishToMavenLocal) {
    group = 'Publishing'
    description = 'Installs artifacts to local Maven repository'
}

//
// RELEASE
//

task release() {
    group 'Project'
    description 'Builds everything for the release.'

    dependsOn clean
    dependsOn build
    dependsOn javadoc
    dependsOn jacocoTestReport
}

